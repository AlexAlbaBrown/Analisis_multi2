****EJERCICIO 5. VARIABLES DUMMY****

/*Para este ejercicio vamos a emplear nuevamente el extracto de variables de la
encuesta aplicada en la Delegación Álvaro Obregón*/

*use "C:\Users\MyDirectory\4B_DAO.dta", clear

*Etiquetamos variables correctamente para saber a qué nos referimos con cada ítem
lab var trayec_embara "Embarazo no deseado"
lab var trayec_robo "Víctima delincuencia"
lab var trayec_salud "Problemas de salud"
lab var trayec_dejapordinero "Deserción escolar $"
lab var trayec_dejaporfam "Deserción escolar familia"
lab var trayec_violesc "Violencia escolar"
lab var trayec_violhogar "Violencia doméstica"
lab var trayec_violpareja "Violencia en la pareja"
lab var trayec_violautoripub "Abuso de autoridad"
lab var trayec_discrimesc "Discriminación escolar"
lab var trayec_discrimcalle "Discriminación callejera"
lab var trayec_violautoriesc "Trato desigual escuela"
lab var trayec_discrimpub "Trato desigual serv.pub."
lab var trayec_adictmia "Adicciones personales"
lab var trayec_adicamigos "Adicciones de amigos"
lab var trayec_adicpadres "Adicciones de familiares"

*Creamos un atajo global
global trayectoria trayec_embara trayec_robo trayec_salud trayec_dejapordinero ///
	trayec_dejaporfam trayec_violesc trayec_violhogar trayec_violpareja trayec_violautoripub ///
	trayec_discrimesc trayec_discrimcalle trayec_violautoriesc trayec_discrimpub ///
	trayec_adictmia trayec_adicamigos trayec_adicpadres

/*Solicitamos una tabulación múltiple de variables. 
Recuerde si el comando "tabm" no funciona, instale el módulo correspondiente de
STATA. Esto lo puede hacer con el comando findit tabchi de  Nicholas J. Cox, 
Durham University, U.K.*/

tabm $trayectoria
tabm $trayectoria, nofreq row

/*Realizamos una recodificación para evitar problemas imputables a las valoraciones
empleadas*/
	
recode $trayectoria (1=1) (2=0)

lab define vul 1 "Me ha sucedido" 0 "No me sucedido"
lab val $trayectoria vul

tabm $trayectoria

/*Ahora sí, comencemos con un análisis de conglomerados a partir del mejor
agrupamiento de variables. En esta ocasión emplearemos la medida de disimilitud
matching*/

cluster completelinkage $trayectoria, measure(Gower) name(cl)
cluster list

cluster tree, cutn(21) showcount xlabel(,angle(90) labsize(*.75))

cluster stop cl, rule(duda) groups(1/10)
cluster stop cl, rule(calinski) groups(2/10)

/*Pese a la indeterminación de nuestras reglas podríamos guiarnos por una 
interpretaciónn intuitiva del cluster tree, donde podemos apreciar tres 
cortes. En virtud de ello, pidamos la especificación de tres grupos*/

cluster generate grp3=group(3),  name(cl)

/*Para intentar tener una visión intuitiva de como se arman nuestros grupos, bien
podríamos ver como se da la aglutinación de variables a partir de una gráfica de
barras. Por tratarse de variables dicotómicas, la frecuencia estará marcada por
el promedio de cada ítem*/

graph bar (mean) $trayectoria, over(grp3)

graph bar $trayectoria, over(grp3)

/*Si albergamos dudas sobre como se da la distribución, podemos recurrir nuevamente
a un tabulado múltiple*/

tabm $trayectoria if grp3==1, row
tabm $trayectoria if grp3==2, row
tabm $trayectoria if grp3==3, row

list $trayectoria if grp3==3, nolabel abbreviate(3)

/*Otra forma de corroborar la distinción es a través del contraste con la media de 
la variabla "index_vulner", el cual es un índice construido a partir de la técnica
de análisis factorial y el cual nos ayuda a tener una idea del grado de eventos
acumulados a lo largo del curso de vida de las y los jóvenes encuestados*/

table grp3, c(mean index_vulner)

/*Si persiste la duda, podemos crear una variable indicativa a partir de la suma
de eventos acumulados en forma cruda. Esto lo podemos hacer mediante la siguiente 
instrucción:*/

egen sumvul=rowtotal($trayectoria)

table grp3, c(mean sumvul median sumvul min sumvul max sumvul)



/*Ahora intentemos realizar el ejercicio mediante el método average linkage*/

cluster averagelinkage $trayectoria, measure(Jaccard) name(avg)
cluster list

cluster tree, cutn(21) showcount xlabel(,angle(90) labsize(*.75))

cluster stop avg, rule(duda) groups(1/10)
/*Con base en la regla duda, podríamos quedarnos con 2 o con 4 grupos*/

cluster stop avg, rule(calinski) groups(2/10)
/*Desde el punto de vista de la regla Calinski podríamos quedarnos con una
solución a 3 grupos.
La decisión no es arbitraria, pues si bien por regla, ambos criterios nos sugieren
quedarnos con 2 grupos, recordemos que la resolución está sujeta a los criterios 
analíticos y ordenadores de quien investiga*/

/*Exploremos la solución a 3 grupos*/
cluster generate avg3=group(2),  name(avg)

/*Para intentar tener una visión intuitiva de como se arman nuestros grupos, bien
podríamos ver como se da la aglutinación de variables a partir de una gráfica de
barras. Por tratarse de variables dicotómicas, la frecuencia estará marcada por
el promedio de cada ítem*/

graph bar (mean) $trayectoria, over(avg3)

graph bar $trayectoria, over(avg3)

/*Mediante la exploración gráfica ya se hacen notar algunos inconvenientes derivados
del encadenamiento medio. Definitivamente, esta solución no es óptima, pero sirve 
para tener una aproximación pedagógica a dos cuestiones. Por un lado, la importancia
de elegir adecuadamente el método de agrupación; y por otro, cuán mala puede ser una 
decisión basada en el average linkage method cuando trabajamos con variables dummy, pues
los promedios no siguen una distribución normal*/
